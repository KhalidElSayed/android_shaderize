
precision mediump float;

uniform float uAmbientFactor;
uniform float uDiffuseFactor;
uniform float uSpecularFactor;
uniform float uShininess;

varying vec3 vPosition;
varying vec3 vNormal;
varying vec3 vNormalEdge;
varying vec3 vColor;
varying vec2 vTexPosition;

void main() {
	vec3 normal = vNormal;	
	vec2 pos = abs(vTexPosition);
	float t = max(pos.x, pos.y);
	if (t > 0.8)  {
		t = (t - 0.8) * 5.0;
		t = t * t * t * (3.0 - 2.0 * t);
		normal = mix(normalize(normal), normalize(vNormalEdge), t);
	}
	
	float diffuse = 0.0;
	float specular = 0.0;
	
	vec3 lightDir = vec3(0.0, 0.0, 5.0) - vPosition;
	vec3 N = normalize(normal);
	vec3 L = normalize(lightDir);
	float lambert = dot(N, L);
	if (lambert > 0.0) {
		diffuse = lambert * uDiffuseFactor;
		
		vec3 R = reflect(-L, N);
		vec3 E = normalize(-vPosition);
		specular = pow(max(dot(R, E), 0.0), uShininess) * uSpecularFactor;
	}
		
	gl_FragColor = vec4(vColor * (uAmbientFactor + diffuse + specular), 1.0);
}
